plugins {
    id 'org.ajoberstar.grgit' version '2.3.0'
    id "org.sonarqube" version "4.0.0.2929"
}

sonarqube {
    properties {
        // SonarQube server URL
        property "sonar.host.url", "http://localhost:9000"

        // Project metadata
        property "sonar.projectKey", "MegaMek"

        // Encoding of source files
        property "sonar.sourceEncoding", "UTF-8"

        // Amount of memory to allocate to the SonarQube analysis process
        property "sonar.jvmArgs", "-Xmx8g -Xms8g"

        property "sonar.log.level", "INFO"
    }
}

allprojects {
    apply plugin: 'java'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' } // SerialKiller master branch
    }
}

subprojects {
    group = 'org.megamek'
    version = '0.48.0'
}

ext {
    mmGitRoot = 'https://github.com/MegaMek/megamek.git'
    // Work on MML or MHQ sometimes requires changes in MM as well. The maven publishing tasks use
    // these properties to append the branch name to the artifact id if the repo is not in the master
    // branch, making it available separately to the child project. 
    mmBranch = grgit.branch.current().name
    mmBranchTag = mmBranch.equals('master')? '' : '-' + mmBranch
}

// A properties_local.gradle file can be used to override any of the above options. For instance,
// rootProject.ext.gitRoot = 'file:///path/to/local/repo' will cause the release target to clone a
// local copy of the repository rather than downloading it.

def localProperties = file('properties_local.gradle')
if (localProperties.exists()) {
    apply from: localProperties
}

